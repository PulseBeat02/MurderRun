plugins {
    id 'java'
    id 'io.github.goooler.shadow' version '8.1.8'
    id 'xyz.jpenilla.run-paper' version '2.3.0'
    id 'org.checkerframework' version '0.6.42'
    id 'com.diffplug.spotless' version '7.0.0.BETA1'
}

apply plugin: 'org.checkerframework'

group = 'io.github.pulsebeat02'
version = '1.21-v1.0.0'
description = 'MurderRun'

repositories {
    mavenLocal()
    mavenCentral()
    maven { url = 'https://repo.papermc.io/repository/maven-public/' }
    maven { url = 'https://oss.sonatype.org/content/groups/public/' }
    maven { url = 'https://maven.enginehub.org/repo/' }
    maven { url = 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url = 'https://oss.sonatype.org/content/repositories/central' }
    maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url = 'https://maven.citizensnpcs.co/repo' }
}

dependencies {

    annotationProcessor 'org.incendo:cloud-annotations:2.0.0-rc.2'

    compileOnly 'org.spigotmc:spigot-api:1.21-R0.1-SNAPSHOT'
    compileOnly 'com.sk89q.worldedit:worldedit-core:7.3.0'
    compileOnly 'com.sk89q.worldedit:worldedit-bukkit:7.3.0'
    compileOnly('net.citizensnpcs:citizens-main:2.0.35-SNAPSHOT') {
        exclude group: '*', module: '*'
    }

    implementation project(':nms-api')
    implementation project(':v1_21')
    implementation 'org.bstats:bstats-bukkit:3.0.2'
    implementation 'net.kyori:adventure-api:4.17.0'
    implementation 'net.kyori:adventure-platform-bukkit:4.3.3'
    implementation 'net.kyori:adventure-text-minimessage:4.17.0'
    implementation 'net.kyori:adventure-text-serializer-plain:4.17.0'
    implementation 'net.kyori:adventure-text-serializer-legacy:4.17.0'
    implementation 'net.kyori:adventure-text-serializer-gson:4.17.0'
    implementation 'team.unnamed:creative-api:1.7.3'
    implementation 'team.unnamed:creative-serializer-minecraft:1.7.3'
    implementation 'team.unnamed:creative-server:1.7.3'
    implementation 'org.incendo:cloud-annotations:2.0.0-rc.2'
    implementation 'org.incendo:cloud-paper:2.0.0-beta.9'
    implementation 'org.incendo:cloud-minecraft-extras:2.0.0-beta.9'
    implementation 'me.lucko:commodore:2.2'
    implementation 'com.openpojo:openpojo:0.9.1'

    testImplementation 'com.github.seeseemelk:MockBukkit-v1.21:3.95.1'
    testImplementation 'team.unnamed:creative-api:1.7.3'
    testImplementation 'team.unnamed:creative-serializer-minecraft:1.7.3'
    testImplementation 'team.unnamed:creative-server:1.7.3'
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

assemble {
    dependsOn 'spotlessApply'
    dependsOn 'shadowJar'
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs.add("-parameters")
    options.encoding = 'UTF-8'
    options.release.set(targetJavaVersion)
    options.fork = true
    options.forkOptions.setMemoryMaximumSize("4g")
}

runServer {
    downloadPlugins {
        url 'https://cdn.modrinth.com/data/S0GwusT4/versions/lPHdbL5F/WETS-0.0.3-bukkit.jar'
        url 'https://cdn.modrinth.com/data/1u6JkXh5/versions/Jo76t1oi/worldedit-bukkit-7.3.5.jar'
    }
    minecraftVersion('1.21')
}

sourceSets {
    main {
        java { srcDir('src/main/java') }
        resources { srcDir('src/main/resources') }
    }
}

processResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    rename('plugin.json', 'plugin.yml')
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.json') {
        expand props
    }
}

shadowJar {
    relocate('org.bstats', 'io.github.pulsebeat02.murderrun.lib.org.bstats')
    relocate('net.kyori', 'io.github.pulsebeat02.murderrun.lib.net.kyori')
    relocate('team.unnamed', 'io.github.pulsebeat02.murderrun.lib.team.unnamed')
    relocate('org.incendo', 'io.github.pulsebeat02.murderrun.lib.org.incendo')
    relocate('me.lucko', "io.github.pulsebeat02.murderrun.lib.me.lucko")
    relocate('io.leangen', "io.github.pulsebeat02.murderrun.lib.io.leangen")
    dependencies {
        exclude(dependency('com.google.code.gson:gson:.*'))
        exclude(dependency('com.mojang:brigadier:.*'))
    }
}

checkerFramework {
    checkers = ['org.checkerframework.checker.nullness.NullnessChecker']
    extraJavacArgs = ['-AsuppressWarnings=uninitialized',
                      '-Astubs=%s/checker-framework/OfflinePlayer.astub'.formatted(projectDir)]
}

spotless {
    java {
        palantirJavaFormat('2.47.0').style("GOOGLE")
    }
}